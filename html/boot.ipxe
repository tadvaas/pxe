#!ipxe
:start
menu Boot Menu
item --gap -- ----------------------

# Common options
item local      Hard Drive
item win11m     Windows Install (Manual Partition)

# Conditionally show items depending on UEFI or BIOS platform
iseq ${platform} efi && goto uefi_menu || goto bios_menu

:uefi_menu
item win11au    UEFI Windows Install (Wipe Hard Drive)
goto end_menu

:bios_menu
item win11al    BIOS Windows Install (Wipe Hard Drive)
goto end_menu

:end_menu
# Shared options for both BIOS and UEFI
item ubuntu2204 Ubuntu 22.04 Server Install
item clonezilla_debian  Boot Clonezilla Debian
item hirens     Boot Hirens BootCD PE
item shredos    Boot ShredOS
item ipxe_shell iPXE Shell
item --gap -- ----------------------
item reboot     Reboot

choose --default win11au --timeout 15000 target && goto ${target}

set base-url http://192.168.0.26/

:local
exit 1

:boot_windows
dhcp
kernel ${base-url}/win11/wimboot
initrd -n unattend.xml ${unattend_file} unattend.xml
initrd -n install.bat ${base-url}/win11/install.bat install.bat
initrd -n winpeshl.ini ${base-url}/win11/winpeshl.ini winpeshl.ini
initrd -n BCD ${base-url}/win11/media/Boot/BCD BCD
initrd -n boot.sdi ${base-url}/win11/media/Boot/boot.sdi boot.sdi
initrd -n boot.wim ${base-url}/win11/media/sources/boot.wim boot.wim
boot

:win11m
set unattend_file ${base-url}/win11/unattend-manual.xml
goto boot_windows

:win11au
set unattend_file ${base-url}/win11/unattend-auto-uefi.xml
goto boot_windows

:win11al
set unattend_file ${base-url}/win11/unattend-auto-legacy.xml
goto boot_windows

:hirens
dhcp
kernel ${base-url}/hirens/wimboot
initrd -n bootmgr ${base-url}/hirens/bootmgr bootmgr
initrd -n BCD ${base-url}/hirens/boot/BCD BCD
initrd -n boot.sdi ${base-url}/hirens/boot/boot.sdi boot.sdi
initrd -n boot.wim ${base-url}/hirens/sources/boot.wim boot.wim
boot

:shredos
dhcp
kernel ${base-url}/shredos/boot/shredos console=tty3 quiet loglevel=0 loadkeys=uk rw \
        nwipe_options="--method=zero --verify=off --noblank --autonuke --autopoweroff" \
        lftp="set sftp:auto-confirm true; open sftp://shredos:shredos@192.168.0.18; \
              cd /home/shredos/reports/; mput *.pdf; \
              cd /home/shredos/logs/; mput nwipe_*.txt;"
boot

:ipxe_shell
shell

:ubuntu2204
dhcp
kernel ${base-url}/ubuntu/casper/vmlinuz 
initrd ${base-url}/ubuntu/casper/initrd
imgargs vmlinuz url=http://192.168.0.26/ubuntu/ubuntu-24.04.2-live-server-amd64.iso boot=casper net.ifnames=0 biosdevname=0 ip=dhcp
boot

:clonezilla_debian
echo Booting Clonezilla (Debian-based)
set cz http://192.168.0.26/clonezilla/live
kernel ${cz}/vmlinuz boot=live union=overlay config components \
       noswap nomodeset noeject edd=on \
       ipv6.disable=1 net.ifnames=0 biosdevname=0 \
       locales=en_GB.UTF-8 keyboard-layouts=gb \
       ocs_live_run="ocs-live-general" ocs_live_batch=no \
	   modules=mpt3sas \
       fetch=${cz}/filesystem.squashfs
initrd ${cz}/initrd.img
boot

:reboot
reboot
